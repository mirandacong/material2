package(default_visibility=["//visibility:public"])

load("@angular//:index.bzl", "ng_package")
load("//third_party/ts/material2/tools:sass_bundle.bzl", "sass_bundle")
load("//:packages.bzl", "MATERIAL_PACKAGES", "MATERIAL_TARGETS", "ROLLUP_GLOBALS",
    "VERSION_PLACEHOLDER_REPLACEMENTS")
load("//third_party/ts/material2/tools:defaults.bzl", "ng_module")

# Root "@angular/material" entry-point.
ng_module(
  name = "material",
  srcs = glob(["*.ts"], exclude=["**/*.spec.ts"]),
  module_name = "@angular/material",
  deps = ["//third_party/ts/material2/src/lib/%s" % p for p in MATERIAL_PACKAGES],
)

sass_bundle(
  name = "theming_bundle",
  # Use the filegroup rules for these sass partials directly because sass_library doesn't
  # act like a filegroup.
  srcs = [
    "//third_party/ts/material2/src/cdk/a11y:a11y_scss_partials",
    "//third_party/ts/material2/src/cdk/overlay:overlay_scss_partials",
    "//third_party/ts/material2/src/cdk/text-field:text_field_scss_partials",
  ] + [
    "//third_party/ts/material2/src/lib/%s:%s_scss_partials" % (p, p.replace('-', '_')) for p in MATERIAL_PACKAGES
  ],
  entry_point = '//third_party/ts/material2/src/lib/core:theming/_all-theme.scss',
  output_name = "_theming.scss",
)

# Creates the @angular/material package published to npm.
ng_package(
  name = "npm_package",
  srcs = ["package.json"],
  entry_point = "src/lib/public_api.js",
  entry_point_name = "material",
  globals = ROLLUP_GLOBALS,
  data = [
    ":theming_bundle",
    "//third_party/ts/material2/src/lib/prebuilt-themes:indigo-pink",
    "//third_party/ts/material2/src/lib/prebuilt-themes:deeppurple-amber",
    "//third_party/ts/material2/src/lib/prebuilt-themes:pink-bluegrey",
    "//third_party/ts/material2/src/lib/prebuilt-themes:purple-green",
  ],
  packages = ["//third_party/ts/material2/src/lib/schematics:npm_package"],
  replacements = VERSION_PLACEHOLDER_REPLACEMENTS,
  deps = MATERIAL_TARGETS,
  # TODO(devversion): Use the npm package for publishing. Right now this is disabled because
  # we build using AOT for serving & testing, but the `ng_package` rule should not include factory
  # files.
  tags = ["manual"],
)
